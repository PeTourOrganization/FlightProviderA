plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.2'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'java-library'
    // JAXB Plugin
    id 'com.intershop.gradle.jaxb' version '7.0.0'
}

group = 'com.example.app'
version = '0.0.1-SNAPSHOT'
description = 'FlightProviderA'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

repositories {
    mavenCentral()
    maven {
        url "https://build.shibboleth.net/nexus/content/repositories/releases/"
    }
}

configurations {
    cxf
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15'

    // Spring Boot WS starter for SOAP services
    implementation 'org.springframework.boot:spring-boot-starter-web-services'
    // Required for WSDL Generation
    implementation 'wsdl4j:wsdl4j'
    // JAXB API
    implementation 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
    // JAXB Runtime (Required engine for Runtime)
    runtimeOnly 'com.sun.xml.bind:jaxb-impl:4.0.0'
    jaxb 'jakarta.xml.bind:jakarta.xml.bind-api:4.0.0'
    jaxb 'com.sun.xml.bind:jaxb-xjc:4.0.0'
    jaxb 'com.sun.xml.bind:jaxb-impl:4.0.0'

//    cxf 'org.apache.cxf:apache-cxf:3.3.6'
    cxf 'org.apache.cxf:apache-cxf:4.1.5'

    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}


tasks.register('FlightDetailsWsdl', JavaExec) {
    ext {
        wsdlDir = file("./src/main/resources")
        wsdlFile = file("${wsdlDir}/OTA_AirFlifoLLS2.1.1RQ.wsdl")
        packageName = "com.example.app.flightprovidera.flightdetails"
        autonameResolution = true
        wsdlLocation = "classpath:/OTA_AirFlifoLLS2.1.1RQ.wsdl"
        outDir = "./src/main/java"
    }
    mainClass = 'org.apache.cxf.tools.wsdlto.WSDLToJava'
    args('-client', '-autoNameResolution', '-p', packageName, '-wsdlLocation', wsdlLocation, '-d', outDir, wsdlFile)
    classpath = configurations.cxf

    // set task inputs/outputs for up-to-date checks
    inputs.file wsdlFile
    outputs.dir outDir
}

tasks.named('test') {
    useJUnitPlatform()
}
